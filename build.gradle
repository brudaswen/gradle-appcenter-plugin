buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.7"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
}

apply plugin: "com.gradle.plugin-publish"
apply plugin: 'java-gradle-plugin'
apply plugin: 'kotlin'
apply plugin: 'maven-publish'

group 'com.betomorrow.gradle'
version '1.2.2'

repositories {
    mavenCentral()
    jcenter()
    google()
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compileOnly gradleApi()

    // Third Party
    compileOnly 'com.android.tools.build:gradle:4.1.1'

    compile 'com.squareup.okhttp3:okhttp:4.9.0'
    compile 'com.squareup.okhttp3:logging-interceptor:4.9.0'
    compile 'com.squareup.retrofit2:converter-gson:2.9.0'
    compile 'com.squareup.retrofit2:retrofit:2.9.0'

    // Testing
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly'org.junit.jupiter:junit-jupiter-engine:5.3.1'

    testCompile 'org.assertj:assertj-core:3.11.1'
    testCompile gradleTestKit()

    testRuntime "cglib:cglib-nodep:3.2.4"        // allows mocking of classes (in addition to interfaces)
    testRuntime "org.objenesis:objenesis:2.4"    // allows mocking of classes without default constructor (together with CGLIB)
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

test {
    useJUnitPlatform {}
}

/**
 * Integration test
 */
sourceSets {
    integrationTest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output

        java.srcDir file("./src/integration-test/kotlin")
        resources.srcDir file("./src/integration-test/resources")
    }
}

task integrationTest(type: Test, group: "verification") {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    mustRunAfter test

    useJUnitPlatform()
}

check.dependsOn integrationTest

gradlePlugin {
    testSourceSets sourceSets.integrationTest
}

/**
 * Publishing
 */

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allSource
}

publishing {
    publications {
        BintrayPublication(MavenPublication) {
            from components.java
            artifact sourceJar
            groupId = "gradle.plugin.${project.group}"
        }
    }
}

wrapper {
    gradleVersion = "6.7.1"
    distributionType = Wrapper.DistributionType.ALL
}

pluginBundle {
    website = 'https://github.com/oliviergauthier/gradle-appcenter-plugin'
    vcsUrl = 'https://github.com/oliviergauthier/gradle-appcenter-plugin'
    tags = ['android', 'appcenter', 'plugin', 'tool']

    plugins {
        libraryPlugin {
            id = 'com.betomorrow.appcenter'
            displayName = 'Gradle AppCenter Plugin'
            description = 'Plugin to upload apk to AppCenter'
        }
    }
}
